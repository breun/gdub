plugins {
  id 'distribution'
  id 'nebula.os-package' version '2.2.0'
  id 'org.ysb33r.bintray' version '1.5'
  id 'com.readytalk.ci' version '0.2.3'
}

group = 'org.dougborg'
version = '0.1.1'

wrapper.gradleVersion = '2.3'

build.dependsOn 'distZip', 'distTar', 'buildDeb', 'buildRpm'
defaultTasks 'build'

ext {
  gdubDescription = '''\
    gdub (gw on the command line) is a gradle / gradlew wrapper. Not to be
    confused with the Gradle Wrapper, gw uses the Gradle Wrapper on projects
    where one is configured, and falls back to use the gradle from the $PATH if
    a wrapper is not available. Also, gw is 66% shorter to type than gradle and
    78% shorter to type than ./gradlew.'''.stripIndent()

  gdubLicense = 'MIT'
  gdubEmail = 'dougborg@dougborg.org'
  gdubAuthor = "Douglas Borg <${gdubEmail}>"
  gdubURL = 'http://gdub.rocks'
}

distributions {
  main {
    contents {
      from projectDir
      include 'bin/*', 'README.md', 'LICENSE', 'install', 'package.json'
    }
  }
}

distTar {
  compression 'gzip'
  extension 'tar.gz'
}

ospackage {
  summary = "Convenience script for gradle / ./gradlew"
  //packageDescription = gdubDescription
  license = gdubLicense
  packager = gdubAuthor
  url = gdubURL
  from('bin') {
    into '/usr/local/bin'
  }
}


import org.ysb33r.gradle.bintray.BintrayGenericUpload
task uploadArchives(type: BintrayGenericUpload) {
  dependsOn distZip, distTar, buildDeb, buildRpm
  onlyIf { isRelease }

  username bintrayUser
  apiKey   bintrayKey

  description gdubDescription
  repoOwner   'dougborg'
  repoName    'gdub'
  packageName name

  tags     'gradle', 'gradlew', 'sh', 'bash'
  vcsUrl   'https://github.com/dougborg/gdub.git'
  licenses gdubLicense

  updatePackage true
  sha1          true
  gpgSign       false // TODO: Sign binaries with gpg

  sources distZip.outputs.files
  sources distTar.outputs.files
}

